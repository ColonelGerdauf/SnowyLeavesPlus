plugins {
    id 'fabric-loom' version '1.0-SNAPSHOT'
    id 'com.modrinth.minotaur' version '2.6.+'
    id 'com.matthewprenger.cursegradle' version '1.4.+'
}

version = project.mod_version
group = project.maven_group

dependencies {
    // Change these versions in gradle.properties
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("*.mod.json") {
        expand "version": project.version
    }
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release = targetJavaVersion
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    archivesBaseName = project.archives_base_name
    withSourcesJar()
}

// TODO: Update this every release
def changelog = "Update to 1.19.3"

import com.modrinth.minotaur.dependencies.ModDependency
modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    versionName = "SnowyLeavesPlus ${version}"
    versionNumber = version
    projectId = "of7wIinq"
    uploadFile = remapJar
    versionType = "beta"
    changelog
    loaders = ["fabric", "quilt"]
    gameVersions = ["1.19", "1.19.1", "1.19.2", "1.19.3"]
    syncBodyFrom = rootProject.file("README.md").text
    // Required dependency Fabric API
    dependencies = [new ModDependency("P7dR8mSH", "required")]
}

curseforge {
    apiKey = System.getenv("CURSEFORGE_TOKEN")
    project {
        id = "611998"
        releaseType = "beta"
        addGameVersion("1.19")
        addGameVersion("1.19.1")
        addGameVersion("1.19.2")
        addGameVersion("1.19.3")
        addGameVersion("Fabric")
        addGameVersion("Quilt")
        mainArtifact(remapJar) {
            changelogType = "markdown"
            changelog
            displayName = "SnowyLeavesPlus ${version}"
        }
        relations {
            requiredDependency("fabric-api")
        }
    }
}
